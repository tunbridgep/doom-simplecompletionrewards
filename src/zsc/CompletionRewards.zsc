class CompletionRewards : EventHandler
{   
    bool got_secrets;
    bool got_items;
    bool got_enemies;
    bool rewarded;

    bool ClassExists(string classname)
    {
        Class<Actor> cls = classname;
        return cls != null;
    }
    
    bool showIntermediateMessages()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_intermediate_messages",myinfo).GetBool();
    }
    
    bool intermediateBold()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_bold_intermediate",myinfo).GetBool();
    }
    
    String intermediateFont()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_font_intermediate",myinfo).GetString();
    }
    
    bool getBold()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_bold_complete",myinfo).GetBool();
    }
    
    String getFont()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_font_complete",myinfo).GetString();
    }
    
    String getCompleteMessage()
    {
        String completemsg = Stringtable.Localize("$COMPLETION_MISSION_COMPLETE");
    
        if (cr_give_rewards == 1)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_HEALTH");
        else if (cr_give_rewards == 2)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_HEALTH");
        else if (cr_give_rewards == 3)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_SOULSPHERE");
        else if (cr_give_rewards == 4)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_MEGASPHERE");
        else if (cr_give_rewards == 5)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_BACKPACK");
        else if (cr_give_rewards == 6)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_ALLMAP");
            
        return completemsg;
    }
    
    void GiveItem(Actor player, string item1, string item2, bool cheat)
    {
        if (ClassExists(item1))
            player.GiveInventory(item1,1, cheat);
        else
            player.GiveInventory(item2,1, cheat);
    }
    
    void RewardAllPlayers()
    {
        //Console.Printf("Rewarding players");
        for(int i = 0;i < players.size();i++)
        {
            if (playeringame[i])
            {
                let player = players[i].mo;
                
                switch (cr_give_rewards)
                {
                    case 1:
                        player.GiveInventory("Health",100);
                        break;
                    case 2:
                        player.GiveInventory("Healthpack",1,true);
                        break;
                    case 3:
                        //player.GiveInventory("Soulsphere",1,true);
                        GiveItem(player,"SoulspherePickup","Soulsphere",true); //Support Simple Inventory Items mod.
                        break;
                    case 4:
                        //player.GiveInventory("Megasphere",1,true);
                        GiveItem(player,"MegaspherePickup","Megasphere",true); //Support Simple Inventory Items mod.
                        break;
                    case 5:
                        //player.GiveInventory("Backpack",1,true);
                        GiveItem(player,"BackpackPickup","Backpack",true); //Support Simple Inventory Items mod.
                        break;
                    case 6:
                        player.GiveInventory("AllmapInventory",1,true);
                        break;
                    default:
                        break;
                }
            }
        }
    }
    
    /*
    private float GetPercentage(int value) //expects a value between 0 and 100 for percentage
    {
        return value / 100.0;
    }
    
    private bool GetRequiredKills()
    {
        let kill_percent = GetPercentage(cr_kills_required);
        return kill_percent > 0 && 
    }
    */
    
    private bool IsTitlemap()
    {
        return gamestate == 7;
    }
    
    override void PlayerEntered (PlayerEvent e)
    {
        //Don't let this thing run on a titlemap
        if (IsTitlemap() && !bDESTROYED)
        {
            Destroy();
            return;
        }
    }
    
    override void WorldTick()
    {
        //Don't run on empty/bad maps
        if (level.total_secrets == 0 || level.total_items == 0 || level.total_monsters <= cr_minimum_enemies)
            return;
    
        let kill_percent = (cr_kills_required / 100.0);
        int required_kills = (level.total_monsters * kill_percent) + 0.5;
        
        let item_percent = (cr_items_required / 100.0);
        int required_items = (level.total_items * item_percent) + 0.5;
        
        let secret_percent = (cr_secrets_required / 100.0);
        int required_secrets = (level.total_secrets * secret_percent) + 0.5;
    
        if (level.found_secrets >= required_secrets && !got_secrets)
        {
            got_secrets = true;
            if (showIntermediateMessages())
                Console.MidPrint(smallfont, Stringtable.Localize("$COMPLETION_INTERMEDIATE_SECRETS"), intermediateBold());
        }
        
        if (level.found_items >= required_items && !got_items)
        {
            got_items = true;
            if (showIntermediateMessages())
                Console.MidPrint(smallfont, Stringtable.Localize("$COMPLETION_INTERMEDIATE_ITEMS"), intermediateBold());
        }
        
        if (level.killed_monsters >= required_kills && !got_enemies)
        {
            got_enemies = true;
            if (showIntermediateMessages())
                Console.MidPrint(smallfont, Stringtable.Localize("$COMPLETION_INTERMEDIATE_MONSTERS"), intermediateBold());
        }
        
        if (got_enemies && got_items && got_secrets && !rewarded)
        {
            rewarded = true;
            RewardAllPlayers();
            Console.MidPrint(bigfont, getCompleteMessage(), getBold());
        }
    }
}
