class CompletionRewards : EventHandler
{   
    bool got_secrets;
    bool got_items;
    bool got_enemies;
    bool rewarded;

    bool ClassExists(string classname)
    {
        Class<Actor> cls = classname;
        return cls != null;
    }
    
    bool showIntermediateMessages()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_intermediate_messages",myinfo).GetBool();
    }
    
    bool intermediateBold()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_bold_intermediate",myinfo).GetBool();
    }
    
    String intermediateFont()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_font_intermediate",myinfo).GetString();
    }
    
    bool getBold()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_bold_complete",myinfo).GetBool();
    }
    
    String getFont()
    {
        PlayerInfo myinfo = players[consoleplayer];
        return CVar.GetCVar("cr_font_complete",myinfo).GetString();
    }
    
    String getCompleteMessage()
    {
        String completemsg = Stringtable.Localize("$COMPLETION_MISSION_COMPLETE");
    
        if (cr_give_rewards == 1)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_HEALTH");
        else if (cr_give_rewards == 2)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_HEALTH");
        else if (cr_give_rewards == 3)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_SOULSPHERE");
        else if (cr_give_rewards == 4)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_MEGASPHERE");
        else if (cr_give_rewards == 5)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_BACKPACK");
        else if (cr_give_rewards == 6)
            return completemsg.." "..Stringtable.Localize("$COMPLETION_ADDING_ALLMAP");
            
        return completemsg;
    }
    
    void GiveItem(Actor player, string item1, string item2, bool cheat)
    {
        if (ClassExists(item1))
            player.GiveInventory(item1,1, cheat);
        else
            player.GiveInventory(item2,1, cheat);
    }
    
    void RewardAllPlayers()
    {
        //Console.Printf("Rewarding players");
        for(int i = 0;i < players.size();i++)
        {
            if (playeringame[i])
            {
                let player = players[i].mo;
                
                switch (cr_give_rewards)
                {
                    case 1:
                        player.GiveInventory("Health",100);
                        break;
                    case 2:
                        player.GiveInventory("Healthpack",100,true);
                        break;
                    case 3:
                        //player.GiveInventory("Soulsphere",1,true);
                        GiveItem(player,"SoulspherePickup","Soulsphere",true); //Support Simple Inventory Items mod.
                        break;
                    case 4:
                        //player.GiveInventory("Megasphere",1,true);
                        GiveItem(player,"MegaspherePickup","Megasphere",true); //Support Simple Inventory Items mod.
                        break;
                    case 5:
                        //player.GiveInventory("Backpack",1,true);
                        GiveItem(player,"BackpackPickup","Backpack",true); //Support Simple Inventory Items mod.
                        break;
                    case 6:
                        player.GiveInventory("AllmapInventory",1,true);
                        break;
                    default:
                        break;
                }
            }
        }
    }
    
    override void WorldTick()
    {
    
        //Don't run on empty/bad maps
        if (level.total_secrets == 0 || level.total_items == 0 || level.total_monsters == 0)
            return;
    
        if (level.found_secrets == level.total_secrets && !got_secrets)
        {
            got_secrets = true;
            if (showIntermediateMessages())
                Console.MidPrint(smallfont, "All Secrets Found", intermediateBold());
        }
        
        if (level.found_items == level.total_items && !got_items)
        {
            got_items = true;
            if (showIntermediateMessages())
                Console.MidPrint(smallfont, "All Items Found", intermediateBold());
        }
        
        if (level.killed_monsters == level.total_monsters && !got_enemies)
        {
            got_enemies = true;
            if (showIntermediateMessages())
                Console.MidPrint(smallfont, "All Monsters Killed", intermediateBold());
        }
        
        if (got_enemies && got_items && got_secrets && !rewarded)
        {
            rewarded = true;
            RewardAllPlayers();
            Console.MidPrint(bigfont, getCompleteMessage(), getBold());
        }
    }
}
